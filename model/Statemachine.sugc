#import "model/Events"
#import "model/States"
#import "model/Transitions"

sugar Statemachine {
	%% Syntax based on:
	%%	   https://github.com/seba--/sugarj/blob/master/case-studies/statemachine/src/statemachine/Metamodel.sugj
	context-free syntax
		Statemachine -> ToplevelDeclaration
		"statemachine" "{" StatemachineBody "}" -> Statemachine {cons("Statemachine")}
		StatemachineInit EventsDec* StateDec* 	-> StatemachineBody {cons("StatemachineBody")}
		
		InitialState InitStm?	-> StatemachineInit {cons("StatemachineInit")}
		"data" CCompoundStm 	-> InitStm {cons("InitStm")}
		
	desugarings
    	desugar-statemachine

  	rules
  
    desugar-statemachine :
      	Statemachine(body@StatemachineBody(_, eventsDec*, stateDec*)) 
      		-> <conc> ([statesEnum, eventsEnum]
      				  , stateFuns
      				  , [main])
      	
      	with
      		statesId := Id("State");
      		eventsId := Id("Event");
      		[statesEnum, stateFuns] := <desugar-state-decs(|statesId)> 	stateDec*;
      		eventsEnum 	:= <desugar-events-decs(|eventsId)> eventsDec*;
      		main 	:= <get-main(|statesId, eventsId)> 	body	     				 	 		 	
      					 	 		 
    get-main(|statesId, eventsId) :
    	body@StatemachineBody(init
    						, eventsDec*
    						, stateDec*)	
    				->	
    					FunDef(DecSpecifierSeq([TypeSpecifier(Int())])
    						 , Declarator(None()
      									, DeclParams(Id("main") , params))
    						 , None()
    						 , StmBlock(block-item*))
    
    	with
    		params := ParamList([ParamDecAbstr(DecSpecifierSeq([TypeSpecifier(Void())])
    										 , None())]);	
    		start-dec := <get-arguments;Hd
    					; get-init-state-dec(|statesId)> init;
    		loop := <get-main-loop(|eventsId)> body;
    		init-stm* := <get-init-stms> init;
    		block-item* :=  <conc> (init-stm*, [start-dec, loop])
    		
	
              
    get-main-loop(|eventsId) :
    	StatemachineBody(_, _, stateDec*)
    		-> 
    		IterationStm(
                While(cond
                	, StmBlock(
                        [ eventDec
                        , SelectionStm(
                            Switch(
                              Expr([PrimaryExpr(Id("state"))])
                            , Block(Some(BlockItems(case*)))
                            )
                          )
                        ]
                      )
                    )
                  )
                   
    	with
    		cond := Expr([PrimaryExpr(Lit(IntLit("1")))]);
    		eventDec := <get-next-event(|eventsId)>;
    		case* := <states-to-cases> stateDec*			
                                        
    state-to-case :
    	State(name, StateBody(_, _, trans*))		
    		-> 	
    		CaseDec(name, SelectionStm(
                          Switch(
                            Expr([PrimaryExpr(Id("event"))])
                          , Block(
                              Some(
                                BlockItems(
                                  eventCase*
                                )
                              )
                            )
                          )
                        )
                      )  
    		
    	with
    		eventCase* := <transitions-to-cases(|name)> trans*;
    		break := JumpStm(Break())
    		
    trans-to-case(|fromState) :
    	Transition(Event(eventName), toState)
    		-> CaseDec(eventName
    				 , StmBlock([exit, change-state, entry])) 
    		
    	with
    		change-state := ExprStm(Some(Expr(
                              [Assign(PrimaryExpr(Id("state"))
                                    , AssignOp()
                                    , PrimaryExpr(toState))]))); 
            
    		exitId 	:= <get-exit-fun-id> fromState;
    		entryId := <get-entry-fun-id> toState;
    		exit  := CallStm(exitId);
    		entry := CallStm(entryId)				
    			                                    
    		
	strategies		
	
	get-init-stms =
		(?StatemachineInit(_, Some(InitStm(StmBlock(stms))))
	    ; !stms)
	   <+ ![]
	
    get-next-event(|eventsId) =
    				!Dec(
                            DecSpecifierSeq([TypeSpecifier(TypedefName(eventsId))])
                          , Some(
                              InitDeclaratorList(
                                [ InitDeclarator(
                                    Declarator(None(), Id("event"))
                                  , Call(PrimaryExpr(Id("readEvent")), None())
                                  )
                                ]
                              )
                            )
                          )
                          
	states-to-cases =
		mapconcat(state-to-case)
		
	transitions-to-cases(|exit) =
		mapconcat(trans-to-case(|exit))	
	  ;	!(<id>, [LabeledStm(Default(JumpStm(Break())))])
	  ; conc	
	    
	overlays
	
	CaseDec(name, stm) = [LabeledStm(
                             Case(
                               ConstExpr(PrimaryExpr(name))
                             , stm
                             )
                           )
                        , JumpStm(Break())]
                          
    StmBlock(stms) = Block(Some(BlockItems(stms)))	 
    
    CallStm(fun) = ExprStm(
                	Some(Expr([Call(PrimaryExpr(fun), None())]))
              	   )                         				 	 		 			 	  		  

}