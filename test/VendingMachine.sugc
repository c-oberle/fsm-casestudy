#import "model/Statemachine"
#import "util/SugarPrint"

statemachine {
	initial state Idle 
	
	data {
		int coins = 0;
		int productsSold = 0;
	}
	
	init { 
		println("--------Start simulation--------");
		println("Initial State: Idle"); 
		println("Events:"); 
		println("0: InsertCoin"); 
		println("1: Abort"); 
		println("2: Confirm"); 
		println("3: TakeProduct");
		println("--------------------------------");  
	}
	
	input {
		Event e = -1;
		while(e < 0 || e > 3) {
			printf("Please enter an event: ");
			scanf("%d", &e);
		}
		return e;	
	}

  	events InsertCoin, Abort, Confirm, TakeProduct

  	state Idle {
  		enter { 
  			println("--------------------------------");
  			println("=> Idle");
  			coins = 0; 
  			println("Coins: %d", coins); 
  			println("--------------------------------");
  		}
    	InsertCoin => CoinInserted
    	Abort => Idle
  	} 
  	state CoinInserted { 
  		enter { 
  			println("--------------------------------");
  			println("=> CoinInserted"); 
  			coins++; 
  			println("Coins: %d", coins);
  			println("--------------------------------"); 
  		}
  		Confirm => GiveProduct
  		InsertCoin => CoinInserted
    	Abort => Idle
  	}
  	state GiveProduct {
  		enter { 
  			println("--------------------------------");
  			println("=> GiveProduct"); 
  			productsSold++; 
  			coins--;
  			println("Products sold: ... %d", productsSold); 
  			println("Change: .......... %d coins", coins);
  			coins = 0;
  			println("--------------------------------");
  		}
    	TakeProduct => Idle
  	}
}